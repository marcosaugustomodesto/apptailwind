/**
 * Register font asset source folder
 */
android.sourceSets.main.assets.srcDirs += file("$buildDir/intermediates/ReactNativeVectorIcons")

/**
 * Task to copy icon font files
 */
afterEvaluate {
    def config = project.hasProperty("vectoricons") ? project.vectoricons : [];
    def iconFontsDir = config.iconFontsDir ?: "../../node_modules/react-native-vector-icons/Fonts";
    def iconFontNames = config.iconFontNames ?: [ "*.ttf" ];

    // Check if the task already exists before creating it
    def fontCopyTask = tasks.findByName("copyReactNativeVectorIconFonts")
    if (fontCopyTask == null) {
        fontCopyTask = tasks.create(
            name: "copyReactNativeVectorIconFonts",
            type: Copy) {
            description = "copy vector icon fonts."
            into "$buildDir/intermediates/ReactNativeVectorIcons/fonts"

            iconFontNames.each { fontName ->
                from(iconFontsDir) {
                    include(fontName)
                }
            }
        }
    }

    android.applicationVariants.all { variant ->
        def targetName = variant.name.capitalize()
        def generateAssetsTask = tasks.findByName("generate${targetName}Assets")
        // Ensure the task exists before trying to access it
        if (generateAssetsTask != null) {
            generateAssetsTask.dependsOn(fontCopyTask)
        }
    }
}